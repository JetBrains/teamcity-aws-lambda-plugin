plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.5.10'
  id 'java'
}

group 'jetbrains.buildServer.clouds.lambda'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  teamcityVersion = hasProperty('teamcityVersion') ? property('teamcityVersion') : '2021.2'
  teamcityDir = hasProperty('teamcity.dir') ? property('teamcity.dir') : "$rootDir/teamcity/servers/TeamCity-${teamcityVersion}"
  teamcityDataDir = "$rootDir/teamcity/data/" + teamcityVersion
  teamcityJavaHome = System.properties['java.home']
  amazonUtilVersion = 'release-99472-133-g49044ab'
  kotlin_version = '1.6.10'
  mavenRepositoryUser = findProperty('maven.repository.user')
  mavenRepositoryPassword = findProperty('maven.repository.password')
  ktor_version = '2.0.0'
}


apply plugin: 'idea'
idea {
  project {
    vcs = 'Git'
  }
}

subprojects {
  group = 'jetbrains.buildServer.lambda'
}

configure(subprojects) {
  apply plugin: 'java'
  apply plugin: 'kotlin'


  sourceCompatibility = "11"
  targetCompatibility = "11"

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  test {
    useTestNG()
    testLogging.showStandardStreams = true
  }

  repositories {
    mavenCentral()
    maven {
      url = 'https://repository.jetbrains.com/all'
    }
    maven {
      url = 'https://packages.jetbrains.team/maven/p/tc/maven'
      credentials {
        username = mavenRepositoryUser
        password = mavenRepositoryPassword
      }
    }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.mockito:mockito-testng:0.4.23"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation 'org.mockito:mockito-inline:4.6.0'
  }


  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}