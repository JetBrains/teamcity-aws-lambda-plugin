buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'org.gradle.crypto.checksum' version '1.4.0'
}

dependencies {
    implementation project(':aws-lambda-common')

    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
    implementation 'com.amazonaws:aws-lambda-java-runtime-interface-client:2.1.0'
    implementation "org.jetbrains.teamcity:agent-api:$teamcityVersion"
    implementation "org.jetbrains.teamcity.internal:agent:${teamcityVersion}"
    implementation "com.amazonaws:aws-java-sdk-s3:1.12.595"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-auth:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'
    testImplementation "org.jetbrains.teamcity:tests-support:${teamcityVersion}"
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://download.jetbrains.com/teamcity-repository'
    }
}

task copyRuntimeDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/dependency'
}

build.dependsOn copyRuntimeDependencies
final resourcesDir = file("$rootDir/aws-lambda-function-resolvers/src/main/resources")


import org.gradle.crypto.checksum.Checksum

task createChecksums(type: Checksum, dependsOn: jar){
    inputFiles.setFrom("$projectDir/build/libs")
    outputDirectory = resourcesDir
    checksumAlgorithm = Checksum.Algorithm.SHA512
    appendFileNameToChecksum.set(true)
}

shadowJar {
    destinationDirectory = resourcesDir
    dependsOn(createChecksums)
}

tasks.withType(AbstractArchiveTask){
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
